# Copyright 2020 The Crashpad Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This is separate from build/config/BUILD.gn to avoid including various .gni
# that it requires, which in turn conflict with settings from BUILDCONFIG.gn
# when building in other trees.

import("./buildflag_header.gni")
import("./platform.gni")

declare_args() {
  is_chromeos_lacros = false
  is_chromeos_ash = false
}

assert(!is_chromeos_lacros || !is_chromeos_ash)
assert(!(is_chromeos_lacros || is_chromeos_ash) || mini_chromium_is_linux)

config("mini_chromium_config") {
  include_dirs = [
    "..",
    root_gen_dir
  ]
}

source_set("build") {
  sources = [ "build_config.h" ]
  public_configs = [ ":mini_chromium_config" ]
}

source_set("buildflag_header_h") {
  sources = [ "buildflag.h" ]
}

buildflag_header("chromeos_buildflags") {
  header = "chromeos_buildflags.h"
  header_dir = "build"

  flags = [
    "IS_CHROMEOS_LACROS=$is_chromeos_lacros",
    "IS_CHROMEOS_ASH=$is_chromeos_ash",
  ]
}
