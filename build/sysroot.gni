# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_mac) {
  declare_args() {
    mac_sdk = ""
    mac_sdk_min = ""
    sysroot = ""
  }

  find_mac_sdk_args = []
  if (mac_sdk != "") {
    find_mac_sdk_args += ["--exact", mac_sdk]
  }
  if (mac_sdk_min != "") {
    find_mac_sdk_args += ["--minimum", mac_sdk_min]
  }
  if (sysroot != "") {
    find_mac_sdk_args += ["--path", sysroot]
  }

  find_mac_sdk_output = exec_script("find_mac_sdk.py", find_mac_sdk_args, "list lines")

  sysroot = find_mac_sdk_output[0]
  mac_sdk = find_mac_sdk_output[1]
} else if (is_fuchsia) {
  declare_args() {
    sysroot = ""
  }
}
